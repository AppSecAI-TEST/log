buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:1.3.0-beta1'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
	}
}

apply plugin: 'com.android.library'
apply plugin: "com.jfrog.bintray"

android {
	compileSdkVersion 'android-21'
	buildToolsVersion '21.1.2'

	defaultConfig {
		minSdkVersion 4
		targetSdkVersion 21
		versionCode 1
		versionName '1.0.0'
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFile getDefaultProguardFile('proguard-android.txt')
		}
	}
}

group = 'co.trikita.log'

task sourcesJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier = 'sources'
}

task javadoc(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives javadocJar
	archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
	user = properties.getProperty("bintray.user")
	key = properties.getProperty("bintray.apikey")
	configurations = ['archives']
	pkg {
		repo = "maven"
		name = "log"
		websiteUrl = 'https://github.com/zserge/log'
		vcsUrl = 'https://github.com/zserge/log'
		licenses = ["MIT"]
		publish = true
		version {
			gpg {
				sign = true
				passphrase = properties.getProperty("bintray.gpg.password")
			}
			mavenCentralSync {
				sync = true
				user = properties.getProperty("bintray.oss.user")
				password = properties.getProperty("bintray.oss.password")
				close = '1'
			}
		}        
	}
}
