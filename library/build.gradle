buildscript {
	repositories {
		jcenter()
	}
	dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
	}
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'co.trikita'
version = '1.0.1'

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar, javadocJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId group
			artifactId 'log'
			version version
			from components.java
			artifact sourcesJar
			artifact javadocJar
			pom.withXml {
				def root = asNode()
				root.appendNode('name', 'log')
				root.appendNode('description', 'Ultimately minimal yet very convenient logger for Android and Java')
				root.appendNode('url', 'https://github.com/zserge/log')
				def scm = root.appendNode('scm')
				scm.appendNode('url', 'https://github.com/zserge/log.git')
				def license = root.appendNode('licenses').appendNode('license')
				license.appendNode('name', 'MIT License')
				license.appendNode('url', 'http://www.opensource.org/licenses/mit-license.php')
			}
		}
	}
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
	user = properties.getProperty("bintray.user")
	key = properties.getProperty("bintray.apikey")
	publications = ['mavenJava']
	pkg {
		repo = "maven"
		name = "log"
		websiteUrl = 'https://github.com/zserge/log'
		vcsUrl = 'https://github.com/zserge/log.git'
		licenses = ["MIT"]
		publish = true
		version {
			name = '1.0.0'
			gpg {
				sign = true
				passphrase = properties.getProperty("bintray.gpg.password")
			}
			mavenCentralSync {
				sync = true
				user = properties.getProperty("bintray.oss.user")
				password = properties.getProperty("bintray.oss.password")
				close = '1'
			}
		}
	}
}
